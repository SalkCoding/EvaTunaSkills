/*
 * This file is part of alkaskills, licensed under the MIT License (MIT).
 *
 * Copyright (c) 2017 Final Child
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.41'
    id 'maven'
    id 'maven-publish'
}

group 'net.alkaonline'
version '0.0.1-SNAPSHOT'

description 'A plugin to create a skill system on Bukkit.'

repositories {
    jcenter()
    mavenLocal()
    maven {
        name 'Final Repository'
        url 'https://dl.bintray.com/finalchild/Final_Repo/'
    }
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
}

dependencies {
    compile (group: 'me.finalchild', name: 'kotlinbukkit', version: '1.0.0-rc.7') {
        exclude group: 'com.destroystokyo.paper', module: 'paper-api'
    }
    compile group: 'com.zaxxer', name: 'HikariCP', version: '3.4.0'
    compileOnly files("C:/Users/Josangbeom/Desktop/Minecraft/Library/Paper-1.15.jar")
    compileOnly files("C:/Users/Josangbeom/Desktop/Minecraft/Library/worldguard-bukkit-7.0.1.jar")
    compileOnly files("C:/Users/Josangbeom/Desktop/Minecraft/Library/worldedit-bukkit-7.0.1.jar")
    compileOnly files("C:/Users/Josangbeom/Desktop/Minecraft/Library/BungeeTabListPlus_BukkitBridge-2.8.11.jar")
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

processResources {
    filteringCharset 'UTF-8'
    expand(
            name: project.name,
            version: project.version,
            description: project.description
    )
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = 'Assembles sources JAR'
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
}

def pomConfig = {
    name project.name
    description project.description
    inceptionYear 2017
    licenses {
        license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'finalchild'
            name 'Final Child'
            email 'finalchild@ourmc.space'
            url 'https://finalchild.me'
            organization 'Our Minecraft Space'
            organizationUrl 'http://ourmc.space'
            roles {
                role 'developer'
            }
            timezone 'Asia/Seoul'
        }
    }

    scm {
        connection 'scm:git:https://github.com/finalchild/alkaskills.git'
        developerConnection 'scm:git:https://github.com/finalchild/alkaskills.git'
        url 'https://github.com/finalchild/alkaskills'
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.children().last() + pomConfig
            }
        }
    }
}

model {
    tasks.generatePomFileForMyPublicationPublication {
        destination = file("${buildDir}/libs/${project.name}-${project.version}.pom")
    }
}
